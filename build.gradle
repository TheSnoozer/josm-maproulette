plugins {
    id 'org.openstreetmap.josm' version '0.8.2'
    id 'java'
    id 'idea'
}

repositories {
    mavenCentral()
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

// fixes a problem with the gradle build
//   org.gradle.api.InvalidUserCodeException: Entry module-info.class is a duplicate but no duplicate
//   handling strategy has been set. Please refer to
//   https://docs.gradle.org/7.4.2/dsl/org.gradle.api.tasks.Copy.html#org.gradle.api.tasks.Copy:duplicatesStrategy
//   for details.
tasks.withType(Jar).all {
    duplicatesStrategy 'exclude'
}

dependencies {
    packIntoJar(project(':maproulette-api'))
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

josm {
    josmCompileVersion = "latest"
    pluginName = "josm-maproulette"
    manifest {
        minJosmVersion = 18427
        minJavaVersion = 11
        description = "The JOSM Maproulette Plugin attempts to make (some) functionallity of maproulette.org available in the JOSM editor"
        mainClass = "io.github.thesnoozer.josm_maproulette.JosmMaproulettePlugin"
        website = new URL("https://github.com/TheSnoozer/josm-maproulette/")
        includeLinksToGithubReleases = true
    }
    github {
        releasesConfig = file("releases.yml")
        repositoryOwner = "TheSnoozer"
        repositoryName = "josm-maproulette"
    }
}
